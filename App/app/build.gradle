apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.gms.google-services'

ext {
    grpcVersion = '1.27.2'
}
android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "com.app.eresearch.meetingtracker"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/DEPENDENCIES'
    }

}
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.3.0'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}


dependencies {
    implementation 'com.google.firebase:firebase-auth:16.0.5'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.navigation:navigation-fragment:2.0.0-rc02'
    implementation 'androidx.navigation:navigation-ui:2.0.0-rc02'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //noinspection GradleCompatible
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'

    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    // gRPC
    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    //implementation "io.grpc:grpc-protobuf-lite:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation 'javax.annotation:javax.annotation-api:1.2'
    protobuf 'com.google.protobuf:protobuf-java:3.3.1'

    //implementation group: 'com.google.api.grpc', name: 'grpc-google-cloud-speech-v1p1beta1', version: '0.75.5'
    implementation group: 'com.google.api.grpc', name: 'grpc-google-cloud-speech-v1', version: '1.22.5'
    implementation group: 'com.google.cloud', name: 'google-cloud-speech', version: '1.22.5'

    // OAuth2 for Google API
    implementation('com.google.auth:google-auth-library-oauth2-http:0.7.0') {
        exclude module: 'httpclient'
    }

    implementation 'androidx.multidex:multidex:2.0.0'

    implementation 'com.google.android.material:material:1.2.0-alpha05'

    implementation 'com.github.qapqap:TimelineView:v1.6'

    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.3.1'

    implementation 'com.loopj.android:android-async-http:1.4.9'

    implementation 'com.github.PhilJay:MPAndroidChart:v2.2.4'

    implementation 'com.github.AnyChart:AnyChart-Android:1.1.2'

    implementation 'javax.mail:javax.mail-api:1.5.3'

    implementation 'com.google.guava:guava:27.0.1-android'

    implementation 'com.dnkilic.waveform:waveform:0.9.3'

    implementation 'com.google.zxing:core:3.4.0'

    implementation 'me.dm7.barcodescanner:zxing:1.9'

    implementation 'androidx.biometric:biometric:1.0.1'
}

task copySecretKey(type: Copy) {
    def File secretKey = file "$System.env.GOOGLE_APPLICATION_CREDENTIALS"
    from secretKey.getParent()
    include secretKey.getName()
    into 'src/main/res/raw'
    rename secretKey.getName(), "credential.json"
}

preBuild.dependsOn(copySecretKey)
